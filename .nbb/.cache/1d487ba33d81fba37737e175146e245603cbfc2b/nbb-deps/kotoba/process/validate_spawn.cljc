(ns kotoba.process.validate-spawn
  "validate-spawn -- addressed on its own.

  Split out of kotoba.lang.process on 2026-09-09 (ADR-2609091200). The unit
  here is the DEFINITION, and this repo's deps.edn names exactly the
  definitions it reaches -- nothing else.
"
  (:require [kotoba.lang.text :as str]
            [kotoba.process.max-arg-bytes :refer [max-arg-bytes]]
            [kotoba.process.max-argv :refer [max-argv]]
            [kotoba.process.max-stdout-bytes :refer [max-stdout-bytes]]
            [kotoba.process.max-timeout-ms :refer [max-timeout-ms]])
  #?(:cljs (:require ["child_process" :as cp]))
  #?(:clj
     (:import (java.io ByteArrayOutputStream InputStream)
              (java.nio.charset StandardCharsets)
              (java.util.concurrent TimeUnit))))

(defn validate-spawn
  "Pure spawn policy. Returns nil when ok, else an error keyword.

  `argv` is a sequential of strings. `allowed` is a set of permitted basenames
  for argv[0], or nil to skip the allowlist (tests only — production hosts
  must pass a set)."
  ([argv max-out timeout] (validate-spawn argv max-out timeout nil))
  ([argv max-out timeout allowed]
   (cond
     (not (sequential? argv)) :process/argv-type
     (empty? argv) :process/empty-argv
     (> (count argv) max-argv) :process/argv-too-long
     (some #(or (not (string? %)) (str/blank? %)
                (> (count %) max-arg-bytes)) argv)
     :process/bad-arg
     (str/includes? (str (first argv)) "/") :process/path-command
     (str/includes? (str (first argv)) "\\") :process/path-command
     (and allowed (not (contains? allowed (first argv)))) :process/not-allowed
     (not (and (integer? max-out) (pos? max-out) (<= max-out max-stdout-bytes)))
     :process/bad-max-stdout
     (not (and (integer? timeout) (pos? timeout) (<= timeout max-timeout-ms)))
     :process/bad-timeout
     :else nil)))
